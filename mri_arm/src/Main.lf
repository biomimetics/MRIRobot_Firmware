target C {
  single-threaded: true,
  platform: {
    name: "STM32",
    board: "F446RE"
  }
}

import QDEC from "lib/Drivers/Encoder.lf"
import USM from "lib/Drivers/USM.lf"
import HomeController from "lib/Home_Controller.lf"
import MotorController from "lib/Motor_Controller.lf"
import ROS_Interface from "lib/ROS_Interface.lf"

preamble {=
  #include <stdio.h>
  #include "stm32f4xx_hal.h"
  #include "stm32_startup.h"
=}

// Import and/or declare reactor classes.
main reactor {
  preamble {=
    extern UART_HandleTypeDef huart2;
    UART_HandleTypeDef huart4;
    UART_HandleTypeDef huart5;
    UART_HandleTypeDef huart6;


    // Target positions
    float target_pos[7] =   {0, 0, 0, 0, 0, 0, 0};
    float target_speed[7] = {0, 0, 0, 0, 0, 0, 0};
    bool target_sel[7] =    {0, 0, 0, 0, 0, 0, 0};
    static int at_home = 0;

    static void MX_GPIO_Init(void) {
      GPIO_InitTypeDef GPIO_InitStruct = {0};
      __HAL_RCC_GPIOA_CLK_ENABLE();
      __HAL_RCC_GPIOB_CLK_ENABLE();
      __HAL_RCC_GPIOH_CLK_ENABLE();

      /* Configure output LED pins: PH0, PH1, PB*/
      HAL_GPIO_WritePin(GPIOH, GPIO_PIN_0|GPIO_PIN_1, GPIO_PIN_RESET);
      GPIO_InitStruct.Pin = GPIO_PIN_0|GPIO_PIN_1;
      GPIO_InitStruct.Mode = GPIO_MODE_OUTPUT_PP;
      GPIO_InitStruct.Pull = GPIO_NOPULL;
      GPIO_InitStruct.Speed = GPIO_SPEED_FREQ_LOW;
      HAL_GPIO_Init(GPIOH, &GPIO_InitStruct);

      HAL_GPIO_WritePin(GPIOA, GPIO_PIN_15, GPIO_PIN_RESET);
      GPIO_InitStruct.Pin = GPIO_PIN_15;
      GPIO_InitStruct.Mode = GPIO_MODE_OUTPUT_PP;
      GPIO_InitStruct.Pull = GPIO_NOPULL;
      GPIO_InitStruct.Speed = GPIO_SPEED_FREQ_LOW;
      HAL_GPIO_Init(GPIOA, &GPIO_InitStruct);

      /* Configure Switch pins : PC0, PC1 */
      GPIO_InitStruct.Pin = GPIO_PIN_0|GPIO_PIN_1;
      GPIO_InitStruct.Mode = GPIO_MODE_INPUT;
      GPIO_InitStruct.Pull = GPIO_NOPULL;
      HAL_GPIO_Init(GPIOC, &GPIO_InitStruct);
    }

    
    static void MX_UART4_Init(void) {
      huart4.Instance = UART4;
      huart4.Init.BaudRate = 115200;
      huart4.Init.WordLength = UART_WORDLENGTH_8B;
      huart4.Init.StopBits = UART_STOPBITS_1;
      huart4.Init.Parity = UART_PARITY_NONE;
      huart4.Init.Mode = UART_MODE_TX_RX;
      huart4.Init.HwFlowCtl = UART_HWCONTROL_NONE;
      huart4.Init.OverSampling = UART_OVERSAMPLING_16;
      HAL_UART_Init(&huart4);
    }

    static void MX_UART5_Init(void) {
      huart5.Instance = UART5;
      huart5.Init.BaudRate = 115200;
      huart5.Init.WordLength = UART_WORDLENGTH_8B;
      huart5.Init.StopBits = UART_STOPBITS_1;
      huart5.Init.Parity = UART_PARITY_NONE;
      huart5.Init.Mode = UART_MODE_TX_RX;
      huart5.Init.HwFlowCtl = UART_HWCONTROL_NONE;
      huart5.Init.OverSampling = UART_OVERSAMPLING_16;
      HAL_UART_Init(&huart5);
    }

    static void MX_UART6_Init(void) {
      huart6.Instance = USART6;
      huart6.Init.BaudRate = 115200;
      huart6.Init.WordLength = UART_WORDLENGTH_8B;
      huart6.Init.StopBits = UART_STOPBITS_1;
      huart6.Init.Parity = UART_PARITY_NONE;
      huart6.Init.Mode = UART_MODE_TX_RX;
      huart6.Init.HwFlowCtl = UART_HWCONTROL_NONE;
      huart6.Init.OverSampling = UART_OVERSAMPLING_16;
      HAL_UART_Init(&huart6);
    }
  =}
  
  timer sys_tick(0 ms, 10 ms)           // Main system clock we will use inside main
  timer switch_motor(0 ms, 1000 ms)     // Switch moto position state
  state count: int = 1

  qdec = new QDEC()                           // FPGA-based QDEC Encoders
  usm = new USM()                             // Ultrasonic motor controllers
  ros_controller = new ROS_Interface()        // ROS UART Interface
  motor_controller = new MotorController()    // Main Motor driver + controller
  home_controller = new HomeController()      // Used to home the motors

  // Route encoder information to relevant reactors
  qdec.qdec_out -> motor_controller.qdec_pos
  qdec.qdec_out -> ros_controller.current_pos
  qdec.sea_out -> motor_controller.qdec_sea
  qdec.sea_out -> ros_controller.sea_pos

  // Route joint targets/information
  motor_controller.current_joint -> ros_controller.current_joint
  motor_controller.speed -> usm.speed
  ros_controller.target_pos -> motor_controller.target_pos_ext

  reaction(startup) {=
    // Prep all GPIO values
    MX_GPIO_Init();

    // Prep UART interfaces
    MX_UART4_Init();
    MX_UART5_Init();
    MX_UART6_Init();
  =}


  initial mode STARTUP {
    reaction(sys_tick) -> qdec.reset_qdec, reset(RUN), reset(HOME) {=
      lf_set(qdec.reset_qdec, true);
      if (HAL_GPIO_ReadPin(GPIOC, GPIO_PIN_0)) {
        printf("\r\n");
        printf("==============================\r\n");
        printf("==== [Startup: Warm Boot] ====\r\n");
        printf("==============================\r\n\r\n");
        lf_set_mode(RUN);
      } else {
        printf("\r\n");
        printf("==============================\r\n");
        printf("==== [Startup: Cold Boot] ====\r\n");
        printf("==============================\r\n\r\n");
        lf_set_mode(HOME);

      }
    =}

  }
  
  mode HOME {
    // +-----------------------------------------------------------------------+
    // | Robot Homing
    // +-----------------------------------------------------------------------+
    qdec.qdec_out -> home_controller.current_pos
    qdec.sea_out -> home_controller.sea_pos
    timer home_pulse(50 ms, 100 ms)
    reaction(home_pulse) -> qdec.reset_qdec, reset(RUN) {=
      if (at_home) {
        for(int i=0; i<7; i++) {
          target_pos[i] = 0;
        }
        lf_set(qdec.reset_qdec, true);
        lf_set_mode(RUN);
        printf("--- Finished Homing ---\r\n");
      }
    =}

    reaction(home_controller.is_home) {=
      at_home = home_controller.is_home->value;
    =}

    reaction(home_controller.motor_speed) {=
      for(int i=0; i<7; i++) {
        target_speed[i] = home_controller.motor_speed[i]->value;
      }
    =}

    reaction(sys_tick) -> motor_controller.target_mode, motor_controller.target_sel, motor_controller.target_speed {=
      for (int i=0; i<7; i++) {
        lf_set(motor_controller.target_sel[i], MODE_VEL);
        lf_set(motor_controller.target_mode, MODE_INT);
        lf_set(motor_controller.target_speed[i], target_speed[i]);
      }
    =}
  }

  // Run state for when the robot is in operation
  mode RUN {
    // +-----------------------------------------------------------------------+
    // | Set the motor control values
    // |    - Switch between velocity or position control
    // |    - Switch between internal or external control
    // +-----------------------------------------------------------------------+
    reaction(sys_tick) -> motor_controller.target_sel, motor_controller.target_mode {=
      for (int i=0; i<7; i++) {
        lf_set(motor_controller.target_sel[i], MODE_POS);   // Position control
        // lf_set(motor_controller.target_sel[i], MODE_VEL);   // Velocity control
      }

      lf_set(motor_controller.target_mode, MODE_INT);   // Internal control moder
      // lf_set(motor_controller.target_mode, MODE_EXT);   // External control moder
    =}



    // +-----------------------------------------------------------------------+
    // | Reactors for debugging/testing
    // +-----------------------------------------------------------------------+
    reaction(switch_motor) {=
      if (self->count == 0) {
        self->count = 1;
      } else if (self->count == 1) {
        self->count = 2;
      } else if (self->count == 2) {
        self->count = 3;
      } else if (self->count == 3) {
        self->count = 4;
      } else {
        self->count = 0;
      }
      printf("switch state: %d\r\n", self->count);
    =}

    // Set the internal target position and/or velocity
    reaction(sys_tick) -> motor_controller.target_pos_int, motor_controller.target_speed {=
      HAL_GPIO_WritePin(GPIOH, GPIO_PIN_1, self->count == 0);
      if (self->count == 0) {
        for (int i=0; i<7; i++) {
          lf_set(motor_controller.target_speed[i], 0);
        }
      } else if (self->count == 1) {
        for (int i=0; i<7; i++) {
          lf_set(motor_controller.target_speed[i], 10);
        }
      } else if (self->count == 2) {
        for (int i=0; i<7; i++) {
          lf_set(motor_controller.target_speed[i], 20);
        }
      } else if (self->count == 3) {
        for (int i=0; i<7; i++) {
          lf_set(motor_controller.target_speed[i], 30);
        }
      } else if (self->count == 4) {
        for (int i=0; i<7; i++) {
          lf_set(motor_controller.target_speed[i], 40);
        }
      } else {
        for (int i=0; i<7; i++) {
          lf_set(motor_controller.target_speed[i], 50);
        }
      }
    =}
  }
}